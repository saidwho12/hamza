#version 450
#extension GL_ARB_separate_shader_objects : enable

#define dot2(v) dot(v,v)
#define saturate(x) clamp(x,0.,1.)
#define FLT_INFINITY 1000000.

layout (local_size_x = 16, local_size_y = 16) in;

layout ( set = 0, binding = 0, rgba8 ) uniform writeonly image2D targetImage;

struct ShapeVertex {
    float x,y,cx,cy,cx1,cy1;
    int type; 
};

layout ( set = 0, binding = 1, std430 ) buffer readonly GlyphData {
    int vertexCount;
    ShapeVertex vertices[];
} glyphData;

const float sdfScale = 30.;

float cross2(in vec2 a, in vec2 b)
{
    return a.x*b.y - a.y*b.x;
}

vec2 B1(in vec2 P0, in vec2 P1, in float t)
{
    return P0 + t * (P1 - P0);
}

float sdLine(in vec2 P0, in vec2 P1, in vec2 P) {
    // P0 + t * (P1 - P0)
    vec2 dv = P1 - P0;
    float t = saturate(dot(P-P0,dv)/dot2(dv));
    vec2 z = (P0+t*dv) - P;
    return sign(cross2(dv,z)) * sqrt(dot2(z));
}

vec2 B2(in vec2 P0, in vec2 p1, in vec2 p2, in float t)
{
    return t*t*p2 + 2.0*t*p1 + P0;
}

vec2 B3(in vec2 P0, in vec2 p1, in vec2 p2, in vec2 p3, in float t)
{
    return t*t*t*p3 + 3.0*t*t*p2 + 3.0*t*p1 + P0;
}

// vec2 derviativeDistToB2(in vec2 p1, in vec2 p2, in float t)
// {
//     return dot(p2,p2)*t*t*t + 3.0*
// }

// Taken from scholarius at https://www.shadertoy.com/view/wts3RX
#define NEWTON_ITER 2
#define HALLEY_ITER 0

float cbrt( float x )
{
    if (sign(x) == 0.0) return 0.0;
    
    float y = sign(x) * uintBitsToFloat( floatBitsToUint( abs(x) ) / 3u + 0x2a514067u );
        
    
    for( int i = 0; i < NEWTON_ITER; ++i )
        y = ( 2. * y + x / ( y * y ) ) * .333333333;

    for( int i = 0; i < HALLEY_ITER; ++i )
    {
        float y3 = y * y * y;
        y *= ( y3 + 2. * x ) / ( 2. * y3 + x );
    }
    
    return y;
}


float distCubicInflectionToExtrema(float p, float q)
{
    return pow(q/2.0,2.0) + pow(p/3.0,3.0);
}

#define FLT_EPSILON 1.e-7
#define PI 3.14159

// Complex cube root
vec3 complexCbrt(vec2 c)
{
    float m = pow(dot(c,c),1.0/6.0); // magnitude
    float t = atan(c.y/c.x);// theta original angle
    float t0 = (t+2.0*PI*0.)/3.0;
    float t1 = (t+2.0*PI*1.0)/3.0;
    float t2 = (t+2.0*PI*2.0)/3.0;
    vec2 z0 = vec2(cos(t0),sin(t0))*m;
    vec2 z1 = vec2(cos(t1),sin(t1))*m;
    vec2 z2 = vec2(cos(t2),sin(t2))*m;
    return vec3(z0[0], z1[0], z2[0]);
}

vec2 complexConjugate(vec2 z)
{
    return vec2(z[0], -z[1]);
}

float sdQuadraticBezier(in vec2 P0, in vec2 P1, in vec2 P2, in vec2 P)
{
    float res;
    
    vec2 p0 = P - P0;
    vec2 p1 = P1 - P0;
    vec2 p2 = P2 - 2.0*P1 + P0;

    // declare coefficients for equation of form t^3 + at^2 + bt + c = 0
    float a = dot(p2,p2);
    float b = (3.0*dot(p1,p2));
    float c = (dot(2.0*p1,p1)-dot(p2,p0));
    float d = -dot(p1,p0);

    // declare y, p and q for depressed cubic form y^3 + py + q = 0 where:
    // t = y - b/(3a)
    // p = c/a - (b^2)/(3a^2) 
    // q = (2b^3)/(27a^3) - (bc)/(3a^2) + d/a
    float p = c/a - (b*b)/(3.0*a*a);
    float q = (2.0*b*b*b)/(27.0*a*a*a) - (b*c)/(3.0*a*a) + d/a;

    // compute the difference between maxima and inflection point, 
    // this tells us how many roots the cubic has.
    float det = pow(q/2.0,2.0) + pow(p/3.0,3.0);
    float h = -b/(3.0*a); // x-offset of inflextion point
    float nt; // nearest t
    float sgn;

    if (det >= 0.0) {
        // 1 root
        det = sqrt(det);
        vec2 x = vec2(det,-det)-q/2.0;
        
        vec2 uv = sign(x)*pow(abs(x),vec2(1.0/3.0));//cbrt(-q/2.0+sqrt(det)) + cbrt(-q/2.0-sqrt(det)) - h;
        float t = clamp(uv.x+uv.y-h,0.0,1.0);
        vec2 v = B2(P0,p1,p2,t) - P;
        sgn = cross2(v, 2.*p2*t+2.*p1);
        res = dot(v,v);
    } else {
        // 3 roots
        // Formula based on the paper on Joukowsky maps and their relation
        // to the cubic equation.
        // https://maa.tandfonline.com/doi/full/10.1080/00029890.2019.1528814#.YRBwTi295B1
        // This solves the three-root case using an equilateral triangle's vertices
        // projected onto the x-axis.
        // h is the point of inflexion on the x-axis.
        // h = -b/(3a)
        // t0 = h+2rcos(theta)
        // t1 = h+2rcos(theta+(2pi)/3)
        // t2 = h+2rcos(theta+(4pi)/3)
        // only the first two of these roots are required.
        float r = sqrt(p/-3.0), theta = acos(q/(-2.0*r*r*r))/3.0;
        float t0 = 2.0*r*cos(theta);
        float t1 = 2.0*r*cos(theta+((2.0*PI)/3.0));
        
        vec2 t = clamp(vec2(t0,t1)-h,0.0,1.0);
        vec2 vx = B2(P0,p1,p2,t.x) - P;
        vec2 vy = B2(P0,p1,p2,t.y) - P;
        float xd = dot(vx,vx);
        float yd = dot(vy,vy);
        
        if (xd < yd) { res = xd; sgn = cross2(vx, 2.*p2*t.x+2.*p1); }
        else { res = yd; sgn = cross2(vy, 2.*p2*t.y+2.*p1); }
    }
    
    // sdDistance(t) = cross2((dBn/dt)(t), P-Bn(t)) * ||Bn(t)-P||
    return sign(sgn) * sqrt(res);
}

void main(void)
{
    ivec2 dim = imageSize(targetImage);
    ivec2 coords = ivec2(gl_GlobalInvocationID.xy);
    vec2 uv = vec2(coords) / dim;

    if (all(lessThan(coords, dim))) {
        vec2 origin = vec2(200,200);
        vec2 off = vec2(0,0);
        vec2 p = vec2(coords);

        float t = FLT_INFINITY;

        // loop over all shape curve vertices
        for (int i = 0; i < glyphData.vertexCount; ++i) {
            float tCurr = FLT_INFINITY;
            ShapeVertex v0 = glyphData.vertices[i], v1 = glyphData.vertices[i+1];

            switch (v0.type) {
                case 1: // moveto
                off = vec2(v0.x,v0.y);
                break;

                case 2: // line segment
                tCurr = sdLine(off,vec2(v0.x,v0.y), p-origin);
                off = vec2(v0.x,v0.y);
                break;

                case 3: // quadratic bezier
                tCurr = sdQuadraticBezier(off, vec2(v0.cx,v0.cy), vec2(v0.x,v0.y), p-origin);
                off = vec2(v0.x,v0.y);
                break;

                case 4: // cubic bezier
                break;

            }

            t = min(t,abs(tCurr));
        }

        imageStore(targetImage, coords, vec4(vec3(t/sdfScale),1.0));
    }
}